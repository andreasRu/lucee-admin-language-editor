/**********************************************************
*  LangEditorService.cfc: 
*  A component used for creating and editing the language 
*  resource XML files used in the Lucee admininistrator. 
*  License: MIT License  
*  (c)2023 C. Andreas RÃ¼ger
*  https://github.com/andreasRu/lucee-admin-language-editor
************************************************************/

component {


    public struct function init( ){
        this.version="0.0.1"
        return this;
    }

    /*********  
    *
    *  An alternative encoding of encodeForXML (specifically XML 1.0) simplified
    *  See: https://stackoverflow.com/a/28152666/2645359 https://www.w3.org/TR/xml/#syntax
    *
    *********/
    public string function encodeXML( string value required ) {
        
        result=arguments.value;
        result=replace( result, "&", "&amp;", "All"); // this MUST go first!!!
        result=replace( result, "<", "&lt;", "All");
        result=replace( result, ">", "&gt;", "All");
        // apostrophes and quotes are allowed in tag bodies (values), so lets add it, 
        // so the same function may be used for attributes
        result=replace( result, """", "&quot;", "All");
        result=replace( result, "'", "&apos;", "All");
        
        return result;
    }

    
    public string function outputAsJson( struct data required) localmode="true"  {
        cfcontent( reset = "true" );
        cfheader( name="content-type", value="application/json");
  	    echo( serializeJSON( arguments.data ) );
  	    cfabort;  
    }

    
    /**
	 * get languages of resource files available in working folder 
	 */
	public array function getLanguagesAvailableInWorkingData( ) localmode="true" {
        result=[];
        result= listToArray( StructKeyList( getFullWorkingData() ) );
        if( !result.isEmpty() ){
            result.delete("en");
            result.prepend('en');
        }
        return result;
    }

    /**
	 * returns a hardcoded lettercode list of available lang resources available at
     * at: https://raw.githubusercontent.com/lucee/Lucee/6.0/core/src/main/cfml/context/admin/resources/language/
	 */
	
    public array function getAvailableLanguagesInLuceeGitSource() {
        
        return [ "de","en","nl","ch-be" ];
    }

   

   

    
    public void function createUpdateWorkingLanguageResourceFile( string languageCode required,  struct formObject ) localmode="true" {

        
        dataKeysXMLCode="";
        if( structKeyExists( arguments, "formObject") ){
            KeyNames= arguments.formObject.fieldnames;
            for( name in local.KeyNames ){
                keyName=replaceNoCase( name, "~", ".", "All" );
                    if( form[ name ] != "" ){
                        dataKeysXMLCode=dataKeysXMLCode & "            <data key=""#encodeXML( keyName )#"">#encodeXML( form[ name ] )#</data>" & chr(10) 
                    }
                }
        }

        if( fileExists(  "/workingPath/#sanitizeFiilename( arguments.languageCode )#.xml") ){ 
            dataXML= getWorkingDataForLanguageByLettercode( arguments.languageCode ); 
        }else{
            // resource file is not available in working dir, so it need to be created a new empty file;

            if( !fileExists(  "/workingPath/en.xml") ){ 
                pullLangResourcesFromGithubToWorkingDirectory( "en" );
                
            }
            dataXML= getWorkingDataForLanguageByLettercode( "en" );
            languageTagName=getAvailableJavaLocalesAsStruct();
            StructUpdate( dataXML[ "xmlRoot" ][ "XmlAttributes" ], "key", arguments.languageCode );
            StructUpdate( dataXML[ "xmlRoot" ][ "XmlAttributes" ], "label", getAvailableJavaLocalesAsStruct()[ arguments.languageCode] );

        }

        dataXML= parseXMLDataToStruct( dataXML );
              

        savecontent variable="xmlCode"{ 
        echo( "<?xml version=""1.0"" encoding=""UTF-8""?>" & chr(10) & 
             "    <!-- File generated by Lucee Admin Language Editor" & this.version & " (Please visit: https://github.com/andreasRu/lucee-admin-language-editor) -->" & chr(10) & 
             "        <language key=""" & encodeXML( dataXML[ "XmlRoot.XmlAttributes.key" ] ) & """ label=""" & encodeXML( dataXML[ "XmlRoot.XmlAttributes.label" ] ) & """>" & chr(10) & 
                                dataKeysXMLCode & chr(10) & 
             "        </language>"
        )
        }

        fileWrite(  "#expandPath("/workingPath")#/#sanitizeFiilename( arguments.languageCode )#.xml",  xmlCode, "utf-8" );
    
    }

    
    public any function downloadFileXML( string languageCode required ) localmode="true" {
    
        if( fileExists( "/workingPath/" & sanitizeFiilename( arguments.languageCode ) & ".xml") ){
            cfheader( name="Content-Disposition", value="attachment; filename=#sanitizeFiilename( arguments.languageCode )#.xml");
            cfcontent( type = "text/xml" file = "/workingPath/" & sanitizeFiilename( arguments.languageCode ) & ".xml" deleteFile = "no" );
        }
        
    }

    

    /**
	 * returns XML data of an imported language resource file as a struct
	 */
	public struct function parseXMLDataToStruct( struct XMLData required ) localmode="true" {
		parsedXMLResult=[:];
        parsedXMLResult["XmlRoot.XmlComment"]=arguments.XMLData.XmlRoot.XmlComment;
		parsedXMLResult["XmlRoot.XmlAttributes.key"]=arguments.XMLData.XmlRoot.XmlAttributes.key;
		parsedXMLResult["XmlRoot.XmlAttributes.label"]=arguments.XMLData.XmlRoot.XmlAttributes.label;
		parsedXMLResult["XmlRoot.XmlAttributes.KeyData"]={};
		loop array="#arguments.XMLData.XmlRoot.XmlChildren#" index="itemChildrenKey" {

		 	keyName=itemChildrenKey["XmlAttributes"]["key"];
		 	parsedXMLResult["XmlRoot.XmlAttributes.KeyData"][ keyName ]=itemChildrenKey.XmlText;

		}

		return parsedXMLResult;
	}

	
	/**
	 * returns as struct of all available 2-letter codes of the underlying java.util with the referring Language DisplayName (target language)
	 */
	public struct function getAvailableJavaLocalesAsStruct() localmode="true" {

		// Get Locale List
		JavaLocale = CreateObject("java", "java.util.Locale");
		availableJavaLocalesArray=JavaLocale.getAvailableLocales();
        //dump( JavaLocale );
		//dump( availableJavaLocalesArray );

        // initialize an ordered struct with shorthand [:]
       
		availableJavaLocalesStruct ={};
        cfloop( array= "#availableJavaLocalesArray#" item="itemLocale" ){
            //echo( dump( [ itemLocale.toLanguageTag(), itemLocale.getDisplayName(), itemLocale.getISO3Language() ] ) );
			if( len( itemLocale.toLanguageTag() ) == 2 )  {
				displayNameTargetLanguage=itemLocale.info();
				    availableJavaLocalesStruct[itemLocale.toLanguageTag()] = UcFirst( displayNameTargetLanguage["display"]["language"] );
				}	 
		}
        // sort by locale;
        result=[:];
        availableJavaLocalesSortedArray=structSort(availableJavaLocalesStruct);
        for( localeLanguage in availableJavaLocalesSortedArray){
            result.insert( localeLanguage, availableJavaLocalesStruct[ localeLanguage ] );
        }
        
        return result;

	}


    /**
	 * copies all known language resource files from Lucees github source to working directory
	 */
	public void function pullLangResourcesFromGithubToWorkingDirectory( string lang required ) localmode="true" {

        for ( language in listToArray( arguments.lang ) ) { 
            fileCopy(   source="https://raw.githubusercontent.com/lucee/Lucee/6.0/core/src/main/cfml/context/admin/resources/language/#language#.xml", 
            destination="#expandPath("/workingPath")#/#language#.xml" );
        }

        if( !fileExists(  "/workingPath/en.xml" ) ){ 
            fileCopy( 
            source="https://raw.githubusercontent.com/lucee/Lucee/6.0/core/src/main/cfml/context/admin/resources/language/en.xml", 
            destination="#expandPath("/workingPath")#/en.xml" );
        }
    }

    
        



    
    public void function pullResourceFileToWebAdmin( string language required ) localmode="true" {

        if( fileExists(  "/workingPath/#sanitizeFiilename( arguments.language )#.xml") ){ 

            fileCopy(   source="/workingPath/#sanitizeFiilename( arguments.language )#.xml", 
            destination="#expandPath("/luceeWebAdmin")#/#sanitizeFiilename( arguments.language )#.xml" );

        }
        
    }


    
    public string function sanitizeFiilename( string filename required ) localmode="true" {
        return reReplaceNoCase( filename,"[^a-zA-Z0-9\-]", "", "ALL"  );
    }

    
   
   public array function getAvailableLangLocalesInWorkingDir() localmode="true" {

        cfdirectory( directory="/workingPath", action="list", name="filequery" );
        result=[];
        for ( file in filequery ) { 
            result.append( listFirst( file.name, "." ) );
        }
        return result;

   }




    /**
	* Clean working directory
	*/
    public void function cleanWorkingDir( string lang ) localmode="true" {
        
        if( !structKeyExists( arguments, "lang" ) || arguments.lang == ""  ){

           langFilesToDelete=getAvailableLangLocalesInWorkingDir();
        
        }else{
           
            langFilesToDelete=[ "#arguments.lang#" ];
        }

        for ( language in langFilesToDelete ) { 
           if( fileExists(  "/workingPath/#sanitizeFiilename( language )#.xml") ){ 
                fileDelete( "/workingPath/#sanitizeFiilename( language )#.xml" );
            }
            
        }

       
    }



    
    public void function cleanWorkingDirAndPullResources( lang ) {

        cleanWorkingDir( lang );
        pullLangResourcesFromGithubToWorkingDirectory( arguments.lang );
        
    }


   
    /**
	* returns XML data of a language resource file as a struct
	*/
	public struct function getWorkingDataForLanguageByLettercode( string languageISOLetterCode required ) localmode="true" {

        myXML=[:];
        xmlString = fileread( "/workingPath/#sanitizeFiilename( arguments.languageISOLetterCode )#.xml", "UTF-8" );
        myXML = xmlParse( xmlString );
        // writeoutput('dddaa');
        // dump(myXML);
        // abort;
         return myXML;
     
    }


    
    /**
	 * iterate all available language resource files  in the working directory return the referenced XML data as a struct
	 */
	public struct function getFullWorkingData() localmode="true" {

        availableWorkingLanguages=getAvailableLangLocalesInWorkingDir();
                
        result={};
        
        for ( langName in availableWorkingLanguages ) { 
            
            result[ langName ] = getWorkingDataForLanguageByLettercode( langName );
            result[ langName ] = parseXMLDataToStruct( result[ langName ] );
            
        }

        return result;

    }


    /**
	* returns the data struct swtiched in such a manner that languages can be iterated to be shown in table
    * columnes and not in table rows
	*/
	public struct function parseDataForTableOutput( struct data required ) localmode="true" {
        result={};
        if( !structIsEmpty( arguments.data ) ){ 
            for( adminkeyName in arguments.data["en"]["XmlRoot.XmlAttributes.KeyData"] ){ 
                result[ adminkeyName ][ "en" ]= arguments.data["en"]["XmlRoot.XmlAttributes.KeyData"][ adminkeyName ];
                for( langCollection in arguments.data ){ 
                    if( langCollection!="en"){
                        result[ adminkeyName ][ langCollection ] = arguments.data[ langCollection ]["XmlRoot.XmlAttributes.KeyData"][ adminkeyName ]?:"";
                    } 
                } 
            }       
        } 

        return  result;
       
    }
}